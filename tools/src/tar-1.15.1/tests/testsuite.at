# Process this file with autom4te to create testsuite. -*- Autotest -*-

# Test suite for GNU tar.
# Copyright (C) 2004 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.

# We need a recent Autotest.
m4_version_prereq([2.52g])

m4_define([AT_TAR_CHECK],[
  m4_foreach([FMT],
             [m4_if([$7],[],[v7,oldgnu,ustar,posix,gnu],[$7])],
             [AT_CHECK([
mkdir FMT
(cd FMT
TAR_OPTIONS="-H FMT"
export TAR_OPTIONS
rm -rf *
$1)],$2,$3,$4,$5,$6)])
])

m4_define([RE_CHECK],[
AT_DATA([$1.re],[$2])
awk '{print NR " " $[]0}' $1.re > $[]$.1
awk '{print NR " " $[]0}' $1 | join $[]$.1 - |
while read NUM RE LINE
do
  echo "$LINE" | grep -- "$RE" >/dev/null || exit 1
done
])

m4_define([AT_SKIP_TEST],[exit 77])

m4_define([AT_STAR_PREREQ],[
test -z "$STAR_TESTSCRIPTS" && AT_SKIP_TEST
test -r "$STAR_TESTSCRIPTS/$1" || AT_SKIP_TEST
])

m4_define([AT_GZIP_PREREQ],[
cat /dev/null | m4_if([$1],[],gzip,[$1]) - > /dev/null 2>&1 || AT_SKIP_TEST
])

AT_INIT

AT_TESTED([tar])

m4_include([version.at])

m4_include([pipe.at])

m4_include([options.at])

m4_include([append.at])

m4_include([delete01.at])
m4_include([delete02.at])
m4_include([delete03.at])
m4_include([delete04.at])
m4_include([delete05.at])

m4_include([extrac01.at])
m4_include([extrac02.at])
m4_include([extrac03.at])
m4_include([extrac04.at])
m4_include([extrac05.at])

m4_include([gzip.at])

m4_include([incremental.at])

m4_include([ignfail.at])

m4_include([link01.at])

m4_include([listed01.at])
m4_include([listed02.at])

m4_include([longv7.at])

m4_include([multiv01.at])
m4_include([multiv02.at])
m4_include([multiv03.at])

m4_include([old.at])

m4_include([recurse.at])

m4_include([same-order01.at])
m4_include([same-order02.at])

m4_include([sparse01.at])

m4_include([volume.at])

m4_include([comprec.at])

m4_include([star/gtarfail.at])
m4_include([star/gtarfail2.at])

m4_include([star/multi-fail.at])

m4_include([star/ustar-big-2g.at])
m4_include([star/ustar-big-8g.at])

m4_include([star/pax-big-10g.at])

