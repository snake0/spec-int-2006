# SPEC Submission Syntax File           CPU2006 Results
#
# Required Variables:
#       syntax  - assoc-array of
#                   keys,
#                   regexps to define acceptable values,
#                   explanatory messages to use in case of mismatch, 
#                   whether only one match is acceptable to pass,
#                    (IOW, if the rule matches for foo000, is it okay for it
#                     to NOT match for foo001, foo002, etc?)
#                   whether to complain only once for this rule,
#                   code ref to do other, more complicated tests
#                    (The code ref gets the current line, the matched key,
#                     the value, the regexp, whether the rule matched, whether
#                     one match is acceptable to pass, whether the value was
#                     already complained about, whether to complain only once
#                     for this rule, and a hash ref to (key, value) pairs for
#                     all the lines in the raw file.)
#        (the last three items are optional and default to false or undef)
# and
#       first   - pattern to recognize first line of submission text
#       last    - pattern to recognize the last line (rest of mail is tossed)
#     or
#       prefix  - pattern to recognize a line in the result file
#
# Note: everything is in Perl syntax, so beware...
#
# Note 2: The explanatory messages may either be one line of text, or an
#         array reference containing multiple lines.  They are evaluated
#         in the context of an interpolated string.  What's that mean?
#         Just that if you want to use double quotes (") in your text,
#         escape them with a backslash.  Also, you may use $key to refer to
#         the key that matched, $k to refer to the specific line that matched,
#         and $value to refer to the value that is in error.
#
# For nicer editing, make your window this wide: ---------------------------------------------------->

undef $first;
undef $last;
$prefix = 'spec\.cpu2006\.';

# Canned error messages for date format fields
my $datere = [ qr/^(:?Jan|Feb|Mar|Apr|May|Jun|Jul|Aug|Sep|Oct|Nov|Dec)-\d{4}$/, 
               [ 
                 '    It must be MMM-YYYY, where YYYY is the full four digit',
                 '    year, and MMM may be any of \"Jan\", \"Feb\", \"Mar\",',
                 '    \"Apr\", \"May\", \"Jun\", \"Jul\", \"Aug\", \"Sep\", \"Oct\", \"Nov\",',
                 '    or \"Dec\".',
                 '   The current value is \"$value\".'
               ],
               0,
               1
             ];

# Yes/No fields
my $yesno = [ qr/^(:?Yes|No)$/,
              [ 
                '    It must be either \"Yes\" or \"No\".',
                '   The current value is \"$value\".'
              ],
              0,
              0
            ];

# This is a sub to do a little extra checking for the memory size.  It's
# a lot like $cachesub below, but simpler.
my $memsub = sub {
    my ($line, $k, $value, $re, $matched, $one_match_sufficient, $complained,
        $complain_once, $values) = @_;

    return () unless $matched;

    # Check for easily-reduceable numbers and units.  If the regexp didn't
    # match in the main code, it won't match here; thus the check above.
    my @gripes = ();
    my %nextunit = ( 'K'=>'M', 'M'=>'G', 'G'=>'T', 'T'=>'P' );
    my %prevunit = ( 'M'=>'K', 'G'=>'M', 'T'=>'G', 'P'=>'T' );
    if ($value =~ /$re/) {
      my ($size, $unit) = ($1, $2);
      if ($size > 1023 && int($size / 1024) == ($size / 1024)) {
          my $newsize = $size / 1024;
          my $newunits = $unit;
          $newunits =~ s/^([KMGT])/$nextunit{$1}/;
          push @gripes, "- Unreduced memory size in the \"$k\" field.";
          push @gripes, '   Please do not use values greater than 1023.';
          push @gripes, "   \"$size $unit\" should be changed to \"$newsize $newunits\"";
          push @gripes, '';
      }
      if ($size < 1024 && ($size < 1 || int($size) != $size)) {
          my $newsize = int($size * 1024);
          my $newunits = $unit;
          $newunits =~ s/^([KMGT])/$prevunit{$1}/;
          push @gripes, "- Fractional memory size in the \"$k\" field.";
          push @gripes, '   Please do not use fractional sizes.';
          push @gripes, "   \"$size $unit\" should be changed to \"$newsize $newunits\"";
          push @gripes, '';
      }
    }
    return @gripes;
};

# Memory.  This is a the basis for the cache
my $memory = [ qr/^([.\d]+) ([KMGT]B)/,
               [ 
                 '    It must contain leading digits, followed by a space,',
                 '    and a standard unit abbreviation.  Acceptable',
                 '    abbreviations are KB, MB, GB, and TB.',
                 '   The current value is \"$value\".'
               ],
               1, # Not all fields need match
               1, # Don't complain more than once
               $memsub,
             ];

# This is a sub to do a little extra checking for cache lines
my $cachesub = sub {
    my ($line, $k, $value, $re, $matched, $one_match_sufficient, $complained,
        $complain_once, $values) = @_;

    return () unless $matched;

    # Check for easily-reduceable numbers and units.  If the regexp didn't
    # match in the main code, it won't match here; thus the check above.
    my @gripes = ();
    my %nextunit = ( 'K'=>'M', 'M'=>'G', 'G'=>'T', 'T'=>'P' );
    my %prevunit = ( 'M'=>'K', 'G'=>'M', 'T'=>'G', 'P'=>'T' );
    my ($place, $per, @sizes, @units);
    ($sizes[0], $units[0], $sizes[1], $units[1], $place, $per) = ($value =~ /$re/);
    for(my $i = 0; $i < 2; $i++) {
        next unless defined($sizes[$i]) && $sizes[$i] ne '';
        if ($sizes[$i] > 1023 && int($sizes[$i] / 1024) == ($sizes[$i] / 1024)) {
            my $newsize = $sizes[$i] / 1024;
            my $newunits = $units[$i];
            $newunits =~ s/^([KMGT])/$nextunit{$1}/;
            push @gripes, "- Unreduced cache size in the \"$k\" field.";
            push @gripes, '   Please do not use values greater than 1023.';
            push @gripes, "   \"$sizes[$i] $units[$i]\" should be changed to \"$newsize $newunits\"";
            push @gripes, '';
        }
        if ($sizes[$i] < 1024 && ($sizes[$i] < 1 || int($sizes[$i]) != $sizes[$i])) {
            my $newsize = int($sizes[$i] * 1024);
            my $newunits = $units[$i];
            $newunits =~ s/^([KMGT])/$prevunit{$1}/;
            push @gripes, "- Fractional cache size in the \"$k\" field.";
            push @gripes, '   Please do not use fractional sizes.';
            push @gripes, "   \"$sizes[$i] $units[$i]\" should be changed to \"$newsize $newunits\"";
            push @gripes, '';
        }
    }
    my $coresperchip = undef;
    $coresperchip = $values->{'hw_ncoresperchip0'} if exists($values->{'hw_ncoresperchip0'});
    $coresperchip = $values->{'hw_ncoresperchip'} if exists($values->{'hw_ncoresperchip'});
    if (defined($coresperchip) && $coresperchip == 1 && $per eq 'core') {
        push @gripes, "- Invalid \"per\" specification for \"$k\".";
        push @gripes, '    Please do not describe caches as "per core"';
        push @gripes, '    for systems that have only one core per chip.';
        push @gripes, '    Instead, please use "per chip".';
        push @gripes, '';
    }

    return @gripes;
};

# Here's the text from osgcpu-18620 which is the basis for the L1 cache memory
# regexp.
#     number      a string of digits
#     space
#     unit        KB, MB, GB, or "K micro-ops"
#     space
#     type        I or I+D
#     [optional
#        " + "
#        number   a string of digits
#        space
#        unit     KB, MB, GB
#        space
#        type     D
#     ]
#     location    "on chip" or "off chip"
#     count       "per core" or "per chip"
#     optional    other text added by tester
my $l1cache = [ qr/^(?:None$|([.\d]+) ([KMGT](?:B| micro-ops)) I(?:\+D| \+ ([.\d]+) ([KMGT]B) D) (on|off) chip per (core|chip))/,
                [
                  '    It must either be set to \"None\" or conform to the',
                  '    following format (in-line spaces are literal):',
                  '       <digits> <unit> I+D <on|off> chip per <chip|core>',
                  '    for a unified cache, or',
                  '       <digits> <unit> I + <digits> <unit> D <on|off> chip per <chip|core>',
                  '    for split caches.',
                  '    Acceptable values for <unit> are KB, MB, GB, TB,',
                  '    or \"K micro-ops\".',
                  '   The current value is \"$value\".'
                ],
                1, # Not all fields need match
                1, # Don't complain more than once
                $cachesub,
              ];

# All other caches have the following format (again, from osgcpu-18620):
#     number      a string of digits
#     space
#     unit        KB, MB, GB
#     space
#     type        I or I+D
#     [optional
#        " + "
#        number   a string of digits
#        space
#        unit     KB, MB, GB
#        space
#        type     D
#     ]
#     location    "on chip" or "off chip"
#     count       "per" whatever
my $cache = [ qr/^(?:None$|([.\d]+) ([KMGT]B) I(?:\+D| \+ ([.\d]+) ([KMGT]B) D) (on|off) chip per (\S+))/,
              [
                '    It must either be set to \"None\" or conform to the',
                '    following format (in-line spaces are literal):',
                '       <digits> <unit> I+D <on|off> chip per <whatever>',
                '    for a unified cache, or',
                '       <digits> <unit> I + <digits> <unit> D <on|off> chip per <whatever>',
                '    for split caches.',
                '    Acceptable values for <unit> are KB, MB, GB, or TB.',
                '   The current value is \"$value\".'
              ],
              1, # Not all fields need match
              1, # Don't complain more than once
              $cachesub,
            ];

# Here's another common one (must contain _something_ that's not whitespace):
my $nonempty = [ qr/\S+/, '- The $k field must not be empty.' ];

# There are a few of these too (empty or digits only)
my $emptyordigits = [ qr/^(?:--|\d*)$/,
                      [ 
                        '    It must be either empty or entirely numeric.',
                        '   The current value is \"$value\".'
                      ]
                    ];

my $digitsonly = [ qr/^\d+$/,
                   [ 
                     '    It must be entirely numeric.',
                     '   The current value is \"$value\".'
                   ]
                 ];

# Here's a canned message for all "shouldn't happen" errors:
my $canthappen = [
                   '    This is not under user control, and should not happen.',
                   '   The current value is \"$value\".'
                 ];

# Here's the sub that checks for (R), (TM), and "processor"
my $tradesub = sub {
    my ($line, $k, $value, $re, $matched, $one_match_sufficient, $complained,
        $complain_once, $values) = @_;
    my @gripes = ();

    # Check for trademark-like characters
    if ($value =~ /(?:\xAE|\x21\x22|\x84\xA2|\x99|\((?i:r|tm|sm)\))/) {
        push @gripes, "- Use of trademark marking in \"$k\" field.";
        push @gripes, '   Please do not use trademark markings like "(R)",';
        push @gripes, '   "(TM)", and "(SM)".  The bottom of each result has';
        push @gripes, '   a footer that explains that these marks are your';
        push @gripes, '   property.  If you must use these markings, do so in';
        push @gripes, '   the notes section.';
        push @gripes, '';
    }

    if ($k =~ /hw_cpu_name/ && $value =~ /processor/i) {
        push @gripes, "- Use of redundant \"processor\" in \"$k\" field.";
        push @gripes, '   Please do not use the word "processor" in the CPU';
        push @gripes, '   name field.  It is implied by context.';
        push @gripes, '';
    }

    return @gripes;
};

# These fields should be checked during review.  (As always.)
%syntax = (
    ##
    ## Settings that the tools insert:
    ##
        'basemean:'     => [ qr/^[.\d]+$/,
                             [ '- Submittable results must include a base run.',
                               '    The basemean ($value) is missing or invalid.'
                             ]
                           ],
        'units:'        => [ qr/^SPEC/, $canthappen ],
        'metric:'       => [ qr/^C(?:INT|FP)2006/, $canthappen ],
        'size:'         => [ qr/^ref$/,
                             [ '- Submittable runs must use the reference workload.',
                               '    Runs using the $value workload are not submittable.'
                             ]
                           ],
        'rawconfig000:' => [ qr/^[\*\@\&]\S{76}$/,
                             [ '- The stored config file is corrupt.',
                               '    This is not under user control, and should not happen.'
                             ]
                           ],
        'invalid:'      => [ qr/^0$/, '- The invalid field is set to \"$value\"; it must be 0.' ],
        'suitever:'     => [ qr/^1\.0$/,
                             [ '- Submittable runs may only be done with v1.0.',
                               '    The result was run with v$value.'
                             ]
                           ],

    ##
    ## Dates (all set by the user):
    ##
        'test_date\d*:' => $datere,
        'hw_avail\d*:'  => $datere,
        'sw_avail\d*:'  => $datere,

    ##
    ## Suite information (set by the user):
    ##
        'license_num\d*:' => $emptyordigits,

    ##
    ## Hardware information for the SUT
    ##
        'hw_cpu_name\d*:'       => $nonempty,
        'hw_cpu_mhz\d*:'        => $digitsonly,
        'hw_disk\d*:'           => $nonempty,
        'hw_fpu\d*:'            => [ qr/\S+/,
                                     [
                                       '    The value must be non-blank.  This field is often',
                                       '    set to \"Integrated\".',
                                     ]
                                   ],
        'hw_memory\d*:'         => $memory,
        'hw_model\d*:'          => $nonempty,
        'hw_nchips\d*:'         => $digitsonly,
        'hw_ncores\d*:'         => $digitsonly,
        'hw_ncoresperchip\d*:'  => $digitsonly,
        'hw_nthreadspercore\d*:'=> $digitsonly,
        'hw_ncpuorder\d*:'      => [ qr/(?:\d+[-,]*)+ \S+/,
                                     [
                                        '    The value must contain a number, list of',
                                       '    numbers, or range of numbers, followed by a unit.',
                                        '   The current value is \"$value\".'
                                     ]
                                   ],
        'hw_other\d*:'          => [ qr/\S+/,
                                     [
                                       '    The value must be non-blank.  If you are not using',
                                       '    any other hardware relevant to SPEC CPU performance,',
                                       '    you can say \"None\" here.'
                                     ]
                                   ],
        'hw_pcache\d*:'         => $l1cache,
        'hw_scache\d*:'         => $cache,
        'hw_tcache\d*:'         => $cache,
        'hw_ocache\d*:'         => $cache,
        'hw_vendor\d*:'         => $nonempty,

    ##
    ## Software information and system state for the SUT
    ##
        'sw_auto_parallel\d*:' => $yesno,
        'sw_compiler\d*:'      => $nonempty,
        'sw_file\d*:'          => $nonempty,
        'sw_os\d*:'            => $nonempty,
        'sw_base_ptrsize\d*:'  => [ qr#^(?:32|64|32/64)-bit$#,
                                    [
                                      '    The value must be either \"32-bit\",',
                                      '    \"64-bit\", or \"32/64-bit\".',
                                      '   The current value is \"$value\".'
                                    ],
                                  ],
        'sw_other\d*:'         => [ qr/\S+/,
                                    [
                                      '    The value must be non-blank.  If you are not using',
                                      '    any other software relevant to SPEC CPU performance,',
                                      '    you can say \"None\" here.'
                                    ]
                                  ],
        'sw_peak_ptrsize\d*:'  => [ qr#^(?:(?:32|64|32/64)-bit|Not Applicable)$#,
                                    [
                                      '    The value must be either \"32-bit\",',
                                      '    \"64-bit\", \"32/64-bit\", or \"Not Applicable\".',
                                      '   The current value is \"$value\".'
                                    ],
                                  ],
        'sw_state\d*:'         => $nonempty,

    ##
    ## Finally, general restrictions
    ##
        '[hs]w_\S+:'           => [ qr/.?/,
                                    [],
                                    0,
                                    0,
                                    $tradesub,
                                  ],
);
1;
